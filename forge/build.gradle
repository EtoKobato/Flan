apply from: "https://raw.githubusercontent.com/Flemmli97/GradleScripts/mc-architectury-1.2/forge.gradle"

loom {
    forge {
        mixinConfigs = ["flan.mixins.json", "flan.forge.mixins.json"]
    }
}

repositories {
    mavenCentral()
    maven {
        name = "FTB"
        url = "https://maven.saps.dev/minecraft"
    }
    maven {
        name = "CurseMaven"
        url "https://www.cursemaven.com"
    }
    maven {
        name = "minecolonies"
        url "https://ldtteam.jfrog.io/ldtteam/modding/"
    }
}

dependencies {
    modCompileOnly "dev.ftb.mods:ftb-ranks-forge:${ftb_ranks}"
    modCompileOnly dicemcmm

    //modRuntimeOnly "dev.ftb.mods:ftb-chunks-forge:${rootProject.ftb_chunks}"
    //CompileOnly cause dependencies
    modCompileOnly "com.ldtteam:minecolonies:${rootProject.minecolonies}"
}

task apiJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set "forge-api-source"
    from project(":common").sourceSets.main.output + sourceSets.main.output
    include "io/github/flemmli97/flan/api/**"
}

task remapApiJar(type: net.fabricmc.loom.task.RemapJarTask) {
    archiveClassifier.set "forge-api"
    input.set apiJar.archiveFile
    addNestedDependencies = false
    dependsOn apiJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapApiJar) {
                classifier "api"
                builtBy remapApiJar
            }
        }
    }
}
