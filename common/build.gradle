plugins {
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

configurations {
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}

repositories {
    maven { url 'https://jitpack.io' }
    maven {
        name = "FTB"
        url = "https://maven.saps.dev/minecraft"
    }
    maven {
        name = "CurseMaven"
        url "https://www.cursemaven.com"
    }
}

dependencies {
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.25'
    shadowCommon group: 'org.yaml', name: 'snakeyaml', version: '1.25'

    modImplementation dynmap_fabric

    modImplementation "dev.ftb.mods:ftb-chunks:${rootProject.ftb_chunks}"
    implementation 'com.github.BlueMap-Minecraft:BlueMapAPI:v2.2.1'
}

architectury {
    common()
}

shadowJar {
    relocate "org.yaml.snakeyaml", "${rootProject.maven_group}.flan.shadow.org.yaml.snakeyaml"
    configurations = [project.configurations.shadowCommon]
}

// Use the shadow jar instead of normal jar to shadow yaml lib properly into fabric builds
transformProductionFabric {
    dependsOn shadowJar
    input.set shadowJar.archiveFile
}

// Use the shadow jar instead of normal jar to shadow yaml lib properly into forge builds
transformProductionForge {
    dependsOn shadowJar
    input.set shadowJar.archiveFile
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(jar) {
                classifier null
                builtBy jar
            }
            artifact(sourcesJar) {
                classifier "sources"
                builtBy remapSourcesJar
            }
            artifactId project.archives_base_name
            version project.version + "-common"
        }
    }

    repositories {
        maven {
            url "https://gitlab.com/api/v4/projects/21830712/packages/maven"
            //url "https://maven.pkg.github.com/flemmli97/<>"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.gitlab.token") ?: System.getenv("GPR_GITLAB_TOKEN")
                //password = project.findProperty("gpr.github.token") ?: System.getenv("GPR_TOKEN")
            }
        }
    }
}